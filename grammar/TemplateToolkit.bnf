{
  psiVisitorName="TemplateToolkitVisitorGenerated"

  tokenTypeClass="com.perl5.lang.tt2.elementTypes.TemplateToolkitTokenType"
  elementTypeClass="com.perl5.lang.tt2.elementTypes.TemplateToolkitElementType"
  elementTypeHolderClass="com.perl5.lang.tt2.lexer.TemplateToolkitElementTypesGenerated"
  elementTypeFactory="com.perl5.lang.tt2.elementTypes.TemplateToolkitElementTypeFactory.getElementType"
  tokenTypeFactory="com.perl5.lang.tt2.elementTypes.TemplateToolkitElementTypeFactory.getTokenType"

  parserUtilClass="com.perl5.lang.tt2.parser.TemplateToolkitParserUtil"
  parserClass="com.perl5.lang.tt2.parser.TemplateToolkitParserGenerated"

  extends="com.perl5.lang.tt2.psi.impl.TemplateToolkitCompositeElementImpl"

  psiClassPrefix="Psi"
  psiImplClassSuffix="Impl"
  psiPackage="com.perl5.lang.tt2.psi"
  psiImplPackage="com.perl5.lang.tt2.psi.impl"

	extends(".+_expr")=expr


//  psiImplUtilClass="com.perl5.lang.tt2.psi.impl.TemplateToolkitPsiImplUtil"

    tokens=[
	TT2_OUTER="TT2_OUTER"
	TT2_HTML="TT2_HTML"
	TT2_COMMENT="TT2_COMMENT"

	TT2_OPEN_TAG="[%"
	TT2_CLOSE_TAG="%]"
	TT2_OUTLINE_TAG="%%"
	TT2_HARD_NEWLINE="OUTLINE_END"

	TT2_IDENTIFIER="TT2_IDENTIFIER"
	TT2_SYMBOL="TT2_SYMBOL"
	TT2_NUMBER="TT2_NUMBER"
	TT2_NUMBER_SIMPLE="TT2_NUMBER_SIMPLE"

	TT2_LEFT_PAREN="("
	TT2_RIGHT_PAREN=")"
	TT2_LEFT_BRACKET="["
	TT2_RIGHT_BRACKET="]"
	TT2_LEFT_BRACE="{"
	TT2_RIGHT_BRACE="}"
	TT2_ASSIGN="="
	TT2_SIGIL_SCALAR="$"

	TT2_SQ="SQ"
	TT2_SQ_OPEN="SQ_OPEN"
	TT2_SQ_CLOSE="SQ_CLOSE"

	TT2_DQ="DQ"
	TT2_DQ_OPEN="DQ_OPEN"
	TT2_DQ_CLOSE="DQ_CLOSE"

	TT2_ESCAPE="\\"
	TT2_STRING_CONTENT="STRING_CONTENT"

	TT2_PERIOD="."
	TT2_COMMA=","

	TT2_EQUAL="=="
	TT2_NOT_EQUAL="!="
	TT2_LT="<"
	TT2_LE="<="
	TT2_GT=">"
	TT2_GE=">="
	TT2_AND="&&"
	TT2_OR="||"
	TT2_NOT="!"
	TT2_QUESTION="?"
	TT2_COLON=":"
	TT2_PLUS="+"
	TT2_MINUS="-"
	TT2_MUL="*"
	TT2_DIV="/"
	TT2_MOD="%"
	TT2_CONCAT="_"

	TT2_DIV_TEXT="div"
	TT2_AND_TEXT="and"
	TT2_OR_TEXT="or"
	TT2_NOT_TEXT="not"
	TT2_MOD_TEXT="mod"

	TT2_GET="GET"
	TT2_CALL="CALL"
	TT2_SET="SET"
	TT2_DEFAULT="DEFAULT"
	TT2_INCLUDE="INCLUDE"
	TT2_PROCESS="PROCESS"
	TT2_WRAPPER="WRAPPER"
	TT2_BLOCK="BLOCK"
	TT2_END="END"
	TT2_IF="IF"
	TT2_UNLESS="UNLESS"
	TT2_ELSIF="ELSIF"
	TT2_ELSE="ELSE"
	TT2_SWITCH="SWITCH"
	TT2_CASE="CASE"
	TT2_FOREACH="FOREACH"
	TT2_WHILE="WHILE"
	TT2_FILTER="FILTER"
	TT2_USE="USE"
	TT2_MACRO="MACRO"
	TT2_PERL="PERL"
	TT2_RAWPERL="RAWPERL"
	TT2_TRY="TRY"
	TT2_THROW="THROW"
	TT2_CATCH="CATCH"
	TT2_FINAL="FINAL"
	TT2_NEXT="NEXT"
	TT2_LAST="LAST"
	TT2_RETURN="RETURN"
	TT2_STOP="STOP"
	TT2_CLEAR="CLEAR"
	TT2_META="META"
	TT2_TAGS="TAGS"
	TT2_DEBUG="DEBUG"

    ]

}

root ::= element *
private element ::= block_element | line_element

private block_element ::= TT2_OPEN_TAG  block_element_content TT2_CLOSE_TAG {pin=1}
private block_element_content ::= directive_expr {recoverWhile=recover_block_element_content}
private recover_block_element_content ::= !(TT2_CLOSE_TAG) // manual recovery needs

private line_element ::= TT2_OUTLINE_TAG line_element_content <<parseHardNewLine>> {pin=1}
private line_element_content ::= directive_expr {recoverWhile=recover_line_element_content}
private recover_line_element_content ::= !(TT2_HARD_NEWLINE) // manual recovery needs

private directive_expr ::=
	call_directive_expr |
	set_directive_expr |
	get_directive_expr

call_directive_expr ::= TT2_CALL expr {pin=1}
get_directive_expr ::= [TT2_GET] expr
private set_directive_expr ::= [TT2_SET] set_directive_item +
set_directive_item ::= identifier_expr TT2_ASSIGN expr {pin=2}

expr ::=
	ternar_expr |
	or_expr |
	and_expr |
	equal_expr |
	compare_expr |
	add_expr |
	mul_expr |
	unary_expr |
	term_expr

ternar_expr ::= expr TT2_QUESTION expr TT2_COLON expr { rightAssociative=true }
or_expr ::= expr ((TT2_OR|TT2_OR_TEXT) expr )+
and_expr ::= expr ((TT2_AND|TT2_AND_TEXT) expr)+
equal_expr ::= expr (TT2_EQUAL|TT2_NOT_EQUAL) expr
compare_expr ::= expr (TT2_LE|TT2_LT|TT2_GT|TT2_GE) expr
add_expr ::= expr ((TT2_PLUS|TT2_MINUS|TT2_CONCAT) expr)+
mul_expr ::= expr ((TT2_MUL|TT2_DIV|TT2_MOD|TT2_DIV_TEXT|TT2_MOD_TEXT) expr)+
unary_expr ::= (TT2_NOT|TT2_NOT_TEXT) expr { rightAssociative=true }

braced_expr ::= TT2_LEFT_BRACE braced_item_content TT2_RIGHT_BRACE {pin=1}
private braced_item_content ::= expr {recoverWhile=recover_braced_item}
private recover_braced_item ::= !(TT2_RIGHT_BRACE)

bracketed_expr ::= TT2_LEFT_BRACKET bracketed_item_content TT2_RIGHT_BRACKET {pin=1}
private bracketed_item_content ::= expr {recoverWhile=recover_bracketed_item}
private recover_bracketed_item ::= !(TT2_RIGHT_BRACKET)

parenthesised_expr ::= TT2_LEFT_PAREN parenthesised_item_content TT2_RIGHT_PAREN {pin=1}
private parenthesised_item_content ::= expr {recoverWhile=recover_parenthesised_item}
private recover_parenthesised_item ::= !(TT2_RIGHT_PAREN)

identifier_expr ::= identifier_expr_content
private identifier_expr_content ::= TT2_IDENTIFIER (TT2_PERIOD deref_part)*
private deref_part ::=
	call_expr |
	variable_expr |
	TT2_NUMBER_SIMPLE |
	keyword_or_identifier_term

variable_expr ::= TT2_SIGIL_SCALAR {variable_braced|identifier_expr_content}
private variable_braced ::= TT2_LEFT_BRACE variable_braced_content TT2_RIGHT_BRACE {pin=1}
private variable_braced_content ::= identifier_expr_content {recoverWhile=recover_braced_variable}
private recover_braced_variable ::= !(TT2_RIGHT_BRACE)

call_expr ::= keyword_or_identifier_term parenthesised_expr

term_expr ::=
	braced_expr |
	bracketed_expr |
	parenthesised_expr |
	call_expr |
	identifier_expr |
	sq_string_expr |
	dq_string_expr |
	TT2_SYMBOL |
	TT2_NUMBER |
	TT2_NUMBER_SIMPLE |
	keyword_or_identifier_term

sq_string_expr ::= TT2_SQ_OPEN sq_string_content TT2_SQ_CLOSE {pin=1}
private sq_string_content ::= TT2_STRING_CONTENT* {recoverWhile=recover_sq_string}
private recover_sq_string ::= !(TT2_SQ_CLOSE)

dq_string_expr ::= TT2_DQ_OPEN dq_string_content TT2_DQ_CLOSE {pin=1}
private dq_string_content ::= TT2_STRING_CONTENT* {recoverWhile=recover_dq_string}
private recover_dq_string ::= !(TT2_DQ_CLOSE)

private keyword_or_identifier_term ::= <<parseIdentifier>>
