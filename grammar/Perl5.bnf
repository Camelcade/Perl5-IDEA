/*
Copyright 2015 Alexandr Evstigneev

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
{
  elementTypeHolderClass="com.perl5.lang.perl.lexer.PerlElementTypesGenerated"
  parserClass="com.perl5.lang.perl.parser.PerlParserGenerated"
  extends="com.perl5.lang.perl.psi.impl.PerlCompositeElementImpl"

  psiClassPrefix="PsiPerl"
  psiImplClassSuffix="Impl"
  psiPackage="com.perl5.lang.perl.psi"
  psiImplPackage="com.perl5.lang.perl.psi.impl"

  elementTypeHolderClass="com.perl5.lang.perl.lexer.PerlElementTypes"

  tokenTypeClass="com.perl5.lang.perl.parser.elementTypes.PerlTokenType"
  elementTypeClass="com.perl5.lang.perl.parser.elementTypes.PerlElementType"

  elementTypeFactory="com.perl5.lang.perl.parser.elementTypes.PerlElementTypeFactory.getElementType"
  tokenTypeFactory="com.perl5.lang.perl.parser.elementTypes.PerlElementTypeFactory.getTokenType"

  parserUtilClass="com.perl5.lang.perl.parser.PerlParserUtil"

  tokens=[
    COMMENT_LINE='COMMENT_LINE'
    COMMENT_ANNOTATION='COMMENT_ANNOTATION'
    COMMENT_BLOCK='COMMENT_BLOCK'
    POD='POD'

    SIGIL_ARRAY='$@'
    SIGIL_SCALAR='$$'
    SIGIL_SCALAR_INDEX='$#'

    // synthetic sigils
    SIGIL_GLOB='$*'
    SIGIL_HASH='$%'
    SIGIL_CODE='$&'

    // generated tokens
    SUB='SUB'
    SCALAR_NAME='SCALAR_NAME'
    ARRAY_NAME='ARRAY_NAME'
    HASH_NAME='HASH_NAME'
    GLOB_NAME='GLOB_NAME'
    CODE_NAME='CODE_NAME'

    HEREDOC='HEREDOC'
    HEREDOC_QQ='HEREDOC_QQ'
    HEREDOC_QX='HEREDOC_QX'
    HEREDOC_END='HEREDOC_END'

    FORMAT='FORMAT'
    FORMAT_TERMINATOR='.'

    VERSION_ELEMENT='VERSION_ELEMENT'

    NUMBER_VERSION='NUMBER_VERSION'
    NUMBER='NUMBER'
    NUMBER_SIMPLE='NUMBER_SIMPLE' // fixme get rid of

    RESERVED_IF='if'
    RESERVED_UNLESS='unless'
    RESERVED_ELSIF='elsif'
    RESERVED_ELSE='else'
    RESERVED_GIVEN='given'
    RESERVED_WHILE='while'
    RESERVED_UNTIL='until'
    RESERVED_FOR='for'
    RESERVED_FOREACH='foreach'
    RESERVED_CONTINUE='continue'
    RESERVED_WHEN='when'
    RESERVED_DEFAULT='default'

    RESERVED_FORMAT='format'
    RESERVED_SUB='sub'
    RESERVED_PACKAGE='package'
    RESERVED_USE='use'
    RESERVED_NO='no'
    RESERVED_REQUIRE='require'

    RESERVED_PRINT='print'
    RESERVED_PRINTF='printf'
    RESERVED_SAY='say'

    RESERVED_MAP='map'
    RESERVED_GREP='grep'
    RESERVED_SORT='sort'

    RESERVED_UNDEF='undef'

    RESERVED_QW='qw'

    RESERVED_QQ='qq'
    RESERVED_Q='q'
    RESERVED_QX='qx'

    RESERVED_TR='tr'
    RESERVED_Y='y'

    RESERVED_S='s'
    RESERVED_QR='qr'
    RESERVED_M='m'

	RESERVED_FINALLY = 'finally';
	RESERVED_TRY = 'try';
	RESERVED_CATCH = 'catch';

    RESERVED_MY='my'
    RESERVED_OUR='our'
    RESERVED_STATE='state'
    RESERVED_LOCAL='local'

    RESERVED_DO='do'
    RESERVED_EVAL='eval'

    RESERVED_GOTO='goto'
    RESERVED_REDO='redo'
    RESERVED_NEXT='next'
    RESERVED_LAST='last'

	RESERVED_RETURN='return'

	RESERVED_METHOD='method'
	RESERVED_FUNC='func'

    // Operators



    OPERATOR_X='x'

    OPERATOR_CMP_NUMERIC='<=>'
    OPERATOR_LT_NUMERIC='<'
    OPERATOR_GT_NUMERIC='>'

    OPERATOR_DEREFERENCE='->'
    FAT_COMMA='=>'
    COMMA=','

    OPERATOR_HELLIP='...'
    OPERATOR_NYI='nyi'
    OPERATOR_FLIP_FLOP='..'
    OPERATOR_CONCAT='.'

    OPERATOR_PLUS_PLUS='++'
    OPERATOR_MINUS_MINUS='--'
    OPERATOR_POW='**'

    OPERATOR_RE='=~'
    OPERATOR_NOT_RE='!~'

    OPERATOR_HEREDOC='heredoc<<'
    OPERATOR_SHIFT_LEFT='<<'
    OPERATOR_SHIFT_RIGHT='>>'

    OPERATOR_AND='&&'
    OPERATOR_OR='||'
    OPERATOR_OR_DEFINED='//'
    OPERATOR_NOT='!'

    OPERATOR_ASSIGN='='

    QUESTION='?'
    COLON=':'

    OPERATOR_REFERENCE='\\'

    OPERATOR_DIV='/'
    OPERATOR_MUL='*'
    OPERATOR_MOD='%'

    OPERATOR_PLUS='+'
    OPERATOR_PLUS_UNARY='u+'

    OPERATOR_MINUS='-'
    OPERATOR_MINUS_UNARY='u-'

    OPERATOR_BITWISE_NOT='~'
    OPERATOR_BITWISE_AND='&'
    OPERATOR_BITWISE_OR='|'
    OPERATOR_BITWISE_XOR='^'

    OPERATOR_AND_LP='and'
    OPERATOR_OR_LP='or'
    OPERATOR_XOR_LP='xor'
    OPERATOR_NOT_LP='not'

    OPERATOR_LT_STR='lt'
    OPERATOR_GT_STR='gt'
    OPERATOR_LE_STR='le'
    OPERATOR_GE_STR='ge'
    OPERATOR_CMP_STR='cmp'
    OPERATOR_EQ_STR='eq'
    OPERATOR_NE_STR='ne'

    // synthetic tokens
    OPERATOR_POW_ASSIGN='**='
    OPERATOR_PLUS_ASSIGN='+='
    OPERATOR_MINUS_ASSIGN='-='
    OPERATOR_MUL_ASSIGN='*='
    OPERATOR_DIV_ASSIGN='/='
    OPERATOR_MOD_ASSIGN='%='
    OPERATOR_CONCAT_ASSIGN='.='
    OPERATOR_X_ASSIGN='x='
    OPERATOR_BITWISE_AND_ASSIGN='&='
    OPERATOR_BITWISE_OR_ASSIGN='|='
    OPERATOR_BITWISE_XOR_ASSIGN='^='
    OPERATOR_SHIFT_LEFT_ASSIGN='<<='
    OPERATOR_SHIFT_RIGHT_ASSIGN='>>='
    OPERATOR_AND_ASSIGN='&&='
    OPERATOR_OR_ASSIGN='||='
    OPERATOR_OR_DEFINED_ASSIGN='//='

    OPERATOR_GE_NUMERIC='>='
    OPERATOR_LE_NUMERIC='<='
    OPERATOR_EQ_NUMERIC='=='
    OPERATOR_NE_NUMERIC='!='
    OPERATOR_SMARTMATCH='~~'
// end of synthetic operators

	OPERATOR_NAMED_UNARY='unary'
	OPERATOR_NAMED_ARGUMENTLESS='argumentless'
    OPERATOR_FILETEST='-t'

    // single mid-quote. e evaluatable s///e;
    REGEX_QUOTE='REGEX_QUOTE'
    REGEX_QUOTE_E='REGEX_QUOTE_E'
    REGEX_TOKEN='REGEX_TOKEN'

    // paired mid-quote. e for evaluatable s{}{}e;
    REGEX_QUOTE_OPEN='REGEX_QUOTE_OPEN'
    REGEX_QUOTE_OPEN_E='REGEX_QUOTE_OPEN_E' // block should be interpolated as a perl script

    REGEX_QUOTE_CLOSE='REGEX_QUOTE_CLOSE'

    REGEX_MODIFIER='REGEX_MODIFIER'
    REGEX_TOKEN='REGEX_TOKEN'

	LP_STRING_Q="LAZY_PARSABLE_Q_STRING"
	LP_STRING_QQ="LAZY_PARSABLE_DQ_STRING"
	LP_STRING_XQ="LAZY_PARSABLE_XQ_STRING"
	LP_STRING_QW="LAZY_PARSABLE_QW_STRING"

	LP_REGEX="LAZY_PARSABLE_REGEX"
	LP_REGEX_X="LAZY_PARSABLE_EXTENDED_REGEX"
	LP_REGEX_REPLACEMENT="LAZY_PARSABLE_REGEX_REPLACEMENT"

	LP_CODE_BLOCK="LAZY_PARSABLE_BLOCK"

    STRING_CONTENT='STRING_CONTENT'
    STRING_CONTENT_QQ='STRING_CONTENT_QQ'
    STRING_CONTENT_XQ='STRING_CONTENT_XQ'

    PARSABLE_STRING_USE_VARS = 'PARSABLE_STRING_USE_VARS'

    TAG='TAG'
    TAG_END='TAG_END'
    TAG_DATA='TAG_DATA'

    LEFT_ANGLE='LEFT_ANGLE'
    RIGHT_ANGLE='RIGHT_ANGLE'

    LEFT_BRACKET='['
    RIGHT_BRACKET=']'

    LEFT_PAREN='('
    RIGHT_PAREN=')'

	// abstract token to make code blocks
    LEFT_BRACE_CODE='code{'

    LEFT_BRACE='{'
    RIGHT_BRACE='}'

    SEMICOLON=';';

    QUOTE_DOUBLE='QUOTE_DOUBLE'
    QUOTE_DOUBLE_OPEN='QUOTE_DOUBLE_OPEN'
    QUOTE_DOUBLE_CLOSE='QUOTE_DOUBLE_CLOSE'

    QUOTE_SINGLE='QUOTE_SINGLE'
    QUOTE_SINGLE_OPEN='QUOTE_SINGLE_OPEN'
    QUOTE_SINGLE_CLOSE='QUOTE_SINGLE_CLOSE'

    QUOTE_TICK='QUOTE_TICK'
    QUOTE_TICK_OPEN='QUOTE_TICK_OPEN'
    QUOTE_TICK_CLOSE='QUOTE_TICK_CLOSE'

    // custom tokens
    IDENTIFIER='IDENTIFIER'

    SUB_PROTOTYPE_TOKEN='SUB_PROTOTYPE_TOKEN'

    PACKAGE_PRAGMA_CONSTANT='constant'
    PACKAGE_PRAGMA_VARS='vars'

    HANDLE='HANDLE'
    BLOCK_NAME='BLOCK_NAME'
    LABEL='LABEL'
  ]

    extends(".+expr")=expr
    name(".+expr")="expression"

	implements(".+_cast_expr")="com.perl5.lang.perl.psi.PerlCastExpression"
	mixin(".+_cast_expr")="com.perl5.lang.perl.psi.mixins.PerlCastExpressionMixin"

	implements("assign_expr")="com.perl5.lang.perl.psi.PerlAssignExpression"
	mixin("assign_expr")="com.perl5.lang.perl.psi.mixins.PerlAssignExpressionImplMixin"

	extends(".*statement_modifier")=statement_modifier

	extends(".*_signature_content")=sub_signature_content

    extends("heredoc_opener|anon_array|anon_hash")=expr
//    extends("glob_slot")=expr
    extends("number_constant")=expr

    extends("string_list")=expr
    mixin("string_list")="com.perl5.lang.perl.psi.mixins.PerlStringListImplMixin"

    extends("replacement_regex|compile_regex|match_regex|tr_regex")=expr

    extends("anon_array_element|heredoc_opener|tag_scalar")=expr

    extends("string_sq|string_dq|string_xq|string_bare")=expr
    implements("string_sq|string_dq|string_xq|string_bare")="com.perl5.lang.perl.psi.PerlString"
    mixin("string_sq|string_dq|string_xq")="com.perl5.lang.perl.psi.mixins.PerlStringImplMixin"
    mixin("string_bare")="com.perl5.lang.perl.psi.mixins.PerlStringBareImplMixin"

    extends("nested_call")=sub_call_expr

    implements("nested_call|sub_call_expr|named_list_expr|unary_list_expr")="com.perl5.lang.perl.psi.PerlMethodContainer"
	mixin("named_list_expr")="com.perl5.lang.perl.psi.mixins.PerlMethodContainerMixIn"

    mixin("use_statement")="com.perl5.lang.perl.psi.mixins.PerlUseStatementImplMixin"
    implements("use_statement")="com.perl5.lang.perl.psi.PerlUseStatement"
    stubClass("use_statement")="com.perl5.lang.perl.idea.stubs.imports.PerlUseStatementStub"

    mixin("no_statement")="com.perl5.lang.perl.psi.mixins.PerlNoStatementImplMixin"
    implements("no_statement")="com.perl5.lang.perl.psi.PerlNoStatement"
    stubClass("no_statement")="com.perl5.lang.perl.idea.stubs.imports.PerlUseStatementStub"

    implements("constant_definition")="com.perl5.lang.perl.psi.PerlConstantDefinition"
    mixin("constant_definition")="com.perl5.lang.perl.psi.mixins.PerlConstantDefinitionMixin"
    stubClass("constant_definition")="com.perl5.lang.perl.idea.stubs.subsdefinitions.PerlSubDefinitionStub"

    extends("use_statement.*")=statement
    extends("no_statement")=statement
    extends("condition_statement|condition_statement_while|for_list_statement|for_iterator_statement")=statement

	implements("named_block|conditional_block|unconditional_block")="com.perl5.lang.perl.psi.properties.PerlStatementsContainer"

	implements("label_declaration")="com.perl5.lang.perl.psi.PerlLabelDeclaration"
	extends("label_declaration")="com.perl5.lang.perl.psi.mixins.PerlLabelDeclarationImplMixin"

	implements("block")="com.perl5.lang.perl.psi.PerlBlock"
	implements("for_compound")="com.perl5.lang.perl.psi.PerlForCompound"
	implements("foreach_compound")="com.perl5.lang.perl.psi.PerlForeachCompound"
	implements("while_compound")="com.perl5.lang.perl.psi.PerlWhileCompound"
	implements("until_compound")="com.perl5.lang.perl.psi.PerlUntilCompound"

    implements("conditional_block_while|.*_compound")="com.perl5.lang.perl.psi.properties.PerlLexicalScope";
    mixin("conditional_block_while|block|.*_compound")="com.perl5.lang.perl.psi.mixins.PerlLexicalScopeMemberMixin"

    implements("heredoc_opener")="com.perl5.lang.perl.psi.PerlHeredocOpener"
    mixin("heredoc_opener")="com.perl5.lang.perl.psi.mixins.PerlHeredocOpenerMixin"

    implements("deref_expr")="com.perl5.lang.perl.psi.PerlDerefExpression"
    mixin("deref_expr")="com.perl5.lang.perl.psi.mixins.PerlDerefExpressionMixin"

    extends("use_vars_statement")=statement
    implements("use_vars_statement")="com.perl5.lang.perl.psi.IPerlUseVars"
    mixin("use_vars_statement")="com.perl5.lang.perl.psi.mixins.PerlUseVarsMixin"

    extends("variable_declaration_global|variable_declaration_lexical|variable_declaration_local")=expr
    implements("variable_declaration_lexical|variable_declaration_local|variable_declaration_global")="com.perl5.lang.perl.psi.PerlVariableDeclaration"
    mixin("variable_declaration_lexical|variable_declaration_local|variable_declaration_global")="com.perl5.lang.perl.psi.mixins.PerlVariableDeclarationMixin"

    extends("code_variable")=expr

    extends("array_array_slice|array_hash_slice|scalar_array_element|scalar_hash_element")=expr

    stubClass("variable_declaration_wrapper")="com.perl5.lang.perl.idea.stubs.variables.PerlVariableStub"
    mixin("variable_declaration_wrapper")="com.perl5.lang.perl.psi.mixins.PerlVariableDeclarationWrapperMixin"
    implements("variable_declaration_wrapper")="com.perl5.lang.perl.psi.PerlVariableDeclarationWrapper"

    extends("array_index_variable|scalar_variable|array_variable|hash_variable|glob_variable")=expr
    mixin("code_variable|scalar_variable|array_variable|hash_variable|array_index_variable")="com.perl5.lang.perl.psi.mixins.PerlVariableImplMixin"
    implements("code_variable|scalar_variable|array_variable|hash_variable|array_index_variable")="com.perl5.lang.perl.psi.PerlVariable"


    stubClass("namespace_definition")="com.perl5.lang.perl.idea.stubs.namespaces.PerlNamespaceDefinitionStub"
    mixin("namespace_definition")="com.perl5.lang.perl.psi.mixins.PerlNamespaceDefinitionImplMixin"
    implements("namespace_definition")="com.perl5.lang.perl.psi.PerlNamespaceDefinition"

    stubClass("method_definition")="com.perl5.lang.perl.idea.stubs.subsdefinitions.PerlSubDefinitionStub"
    mixin("method_definition")="com.perl5.lang.perl.psi.mixins.PerlMethodDefinitionImplMixin"
    implements("method_definition")="com.perl5.lang.perl.psi.PerlMethodDefinition"

    stubClass("func_definition")="com.perl5.lang.perl.idea.stubs.subsdefinitions.PerlSubDefinitionStub"
    mixin("func_definition")="com.perl5.lang.perl.psi.mixins.PerlFuncDefinitionImplMixin"
    implements("func_definition")="com.perl5.lang.perl.psi.PerlFuncDefinition"

    stubClass("sub_definition")="com.perl5.lang.perl.idea.stubs.subsdefinitions.PerlSubDefinitionStub"
    mixin("sub_definition")="com.perl5.lang.perl.psi.mixins.PerlSubDefinitionImplMixin"
    implements("sub_definition")="com.perl5.lang.perl.psi.PerlSubDefinition"

    stubClass("sub_declaration")="com.perl5.lang.perl.idea.stubs.subsdeclarations.PerlSubDeclarationStub"
    mixin("sub_declaration")="com.perl5.lang.perl.psi.mixins.PerlSubDeclarationImplMixin"
    implements("sub_declaration")="com.perl5.lang.perl.psi.PerlSubDeclaration"

    stubClass("glob_variable")="com.perl5.lang.perl.idea.stubs.globs.PerlGlobStub"
    mixin("glob_variable")="com.perl5.lang.perl.psi.mixins.PerlGlobVariableImplMixin"
    implements("glob_variable")="com.perl5.lang.perl.psi.PerlGlobVariable"

    mixin("require_expr")="com.perl5.lang.perl.psi.mixins.PerlRequireExprImplMixin"
    implements("require_expr")="com.perl5.lang.perl.psi.PerlRequireExpr"
    stubClass("require_expr")="com.perl5.lang.perl.idea.stubs.imports.runtime.PerlRuntimeImportStub"

	implements("grep_expr")="com.perl5.lang.perl.psi.PerlGrepExpr"
	implements("map_expr")="com.perl5.lang.perl.psi.PerlMapExpr"
	implements("sort_expr")="com.perl5.lang.perl.psi.PerlSortExpr"
	implements("eval_expr")="com.perl5.lang.perl.psi.PerlEvalExpr"
	implements("sub_expr")="com.perl5.lang.perl.psi.PerlSubExpr"

    mixin("do_expr")="com.perl5.lang.perl.psi.mixins.PerlDoExprImplMixin"
    implements("do_expr")="com.perl5.lang.perl.psi.PerlDoExpr"
    stubClass("do_expr")="com.perl5.lang.perl.idea.stubs.imports.runtime.PerlRuntimeImportStub"

    mixin("method")="com.perl5.lang.perl.psi.mixins.PerlMethodImplMixin";
    implements("method")="com.perl5.lang.perl.psi.PerlMethod";

    implements("namespace_content")="com.perl5.lang.perl.psi.properties.PerlStatementsContainer"
 }

///////////////////////////// main code structure //////////////////////////////////////////////////////////////////////
root ::= <<parseFileContent>> file_items
// invoked by parser
private file_items ::= file_item*

private file_item ::= !<<eof>>
{
	namespace_definition
	| label_declaration [statement_item]
    | statement_item
}

private statement_item ::=
        <<parseSemicolon>> +
        | nyi_statement
        | <<parseParserExtensionStatement>>
        | compound_statement
        | format_definition
        | statement
        | block_compound  // put it after statement to handle anon hashes before it.
        | annotation
		| POD
		| TAG_END
		| TAG_DATA
		| <<parseBadCharacters>> // Fallback for bad characters

namespace_definition ::= namespace_definition_name (block | <<parseSemicolon>> <<parseNamespaceContent>>) {pin=1 recoverWhile=recover_statement}
private namespace_definition_name ::= 'package' IDENTIFIER [perl_version] {pin=1 recoverWhile=recover_statement}
namespace_content ::= real_namespace_content
private real_namespace_content ::= {!'package' file_item} *  {extends=block recoverWhile=recover_statement} //

private parse_block ::= LP_CODE_BLOCK | block
block ::= '{' block_content '}' {pin=1} // !-> pin disabled for dereference of anon hash; probably this slows us down
private block_content ::= file_item * {recoverWhile=recover_statement}

nyi_statement ::= 'nyi'
format_definition ::=  'format' [IDENTIFIER] '=' [FORMAT] FORMAT_TERMINATOR {pin=1}

private compound_statement ::=
    sub_definition
    | named_block
    | if_compound
    | unless_compound
    | given_compound
    | while_compound
    | until_compound
    | for_compound
    | foreach_compound
    | when_compound
    | default_compound

named_block ::= BLOCK_NAME parse_block

if_compound ::=  'if' conditional_block if_compound_elsif * [if_compound_else]  {pin=1}
unless_compound ::=  'unless' conditional_block if_compound_elsif * [if_compound_else]  {pin=1}
private if_compound_elsif ::= [POD]  'elsif' conditional_block  {pin=2}
private if_compound_else ::= [POD]  'else' unconditional_block {pin=2}
unconditional_block ::= parse_block

// implements Try::Tiny or Error fallback
trycatch_expr ::= try_branch (catch_branch|finally_branch)* [expr]
try_branch ::= 'try' (parse_block|expr) {pin=1}
catch_branch ::= 'catch' (parse_block|expr) {pin=1}
finally_branch ::= 'finally' (parse_block|expr) {pin=1}

conditional_block ::= condition_statement parse_block {pin=1 name="Conditional block"}
condition_statement ::= '(' condition_statement_expr ')' {pin=1 name="Condition"}

conditional_block_while ::= condition_statement_while parse_block {pin=1 name="Conditional block"}
condition_statement_while ::= '(' [condition_statement_expr] ')' {pin=1 name="Condition"}
private condition_statement_expr ::= expr {recoverWhile=recover_condition_statement_expr}
private recover_condition_statement_expr ::= !('{'|')')

given_compound ::=  'given' conditional_block  {pin=1}
when_compound ::=  'when' conditional_block  {pin=1}
default_compound ::=  'default' parse_block {pin=1}

while_compound ::=  'while' conditional_block_while [continue_block]  {pin=1}
until_compound ::=  'until' conditional_block [continue_block]  {pin=1}

continue_block ::= [POD] 'continue' parse_block {pin=2}

private block_compound ::= parse_block [continue_block]


// for/foreach
// fixme why the heck there is no parsing error on "for" and there is a error on "use", both pinned
for_compound ::=  'for' for_arguments {pin=1}
foreach_compound ::=  'foreach' for_arguments  {pin=1}
private for_arguments ::=
    for_iterator parse_block
    | for_list_statement block_compound

for_iterator ::= '(' [for_iterator_statement]  ';' [for_iterator_statement] ';' [for_iterator_statement] ')' {pin=3}
for_iterator_statement ::= expr {recoverWhile=recover_parenthesised}

// @todo only single variable or variable definition may be here
for_list_statement ::= [ variable_declaration | variable ] for_list_epxr
for_list_epxr ::= '(' for_list_expr_content ')' {pin=1}
private for_list_expr_content ::= list_expr {recoverWhile=recover_parenthesised}


statement ::= statement_body <<statementSemi>>
private statement_body ::=
    use_statements
    | no_statements
    | sub_declaration
    | normal_statement
    {recoverWhile=recover_statement}

private normal_statement ::= expr [<<parseStatementModifier>>] {pin=1}
//private normal_statement_body ::= expr {recoverWhile=recover_statement}

// fixme we can't pin this because we need to distinct declaration and definition
sub_definition ::=  'sub' IDENTIFIER sub_definition_parameters parse_block

private sub_definition_parameters ::= [sub_prototype_or_signature] [sub_attributes]
private sub_prototype_or_signature ::= sub_signature |sub_prototype

sub_declaration ::=  'sub' IDENTIFIER sub_declaration_parameters
private sub_declaration_parameters ::= [sub_prototype] [sub_attributes]

// todo implement parsing
private sub_prototype ::= '(' [sub_prototype_content] ')' {pin=1}
private sub_prototype_content ::= <<parseSubPrototype>>

/************************************* Sub signatures *****************************************************************/
private sub_signature ::= '(' sub_signature_content ')' //{pin=1} pinning is temporary disabled in favor of protos
sub_signature_content ::=
	<<cancelProtoLikeSignature>>
	{
		sub_signature_element (',' sub_signature_element)* [',' sub_signature_element_slurpy]
		| [sub_signature_element_slurpy]
	}
//	{recoverWhile=recover_signature_content}
private sub_signature_element ::= sub_signature_element_ignore	| sub_signature_scalar
private sub_signature_element_slurpy ::= <<arrayDeclarationWrapper>> | <<hashDeclarationWrapper>>

// fixme this is side-effect of detecting var names in lexer
sub_signature_element_ignore ::= SIGIL_SCALAR [<<checkAssignIdentifier>> [scalar_expr]] &(','|')')
private sub_signature_scalar ::= <<scalarDeclarationWrapper>> ['=' scalar_expr]
/************************************* Sub signatures *****************************************************************/

private sub_attributes ::= ':' attribute ([':'] attribute)*  {pin=1}
private var_attributes ::= ':' attribute ([':'] attribute)*  // {pin=1} pin disable because of $something ? my $var : $other;
attribute ::= IDENTIFIER [quoted_sq_string]

last_expr ::=  'last' [lnr_param] {pin=1}
next_expr ::=  'next' [lnr_param] {pin=1}
redo_expr ::=  'redo' [lnr_param] {pin=1}
goto_expr ::=  'goto' [goto_param] {pin=1}

return_expr ::=  'return' [list_expr] {pin=1}

private lnr_param ::= <<convertIdentifier 'LABEL '>> | expr // fixme scalar_expr ?
private goto_param ::= <<convertIdentifier 'LABEL '>> | '&' <<convertIdentifier 'SUB '>> | expr // fixme same, scalar expr?


statement_modifier ::=
    if_statement_modifier
    | unless_statement_modifier
    | while_statement_modifier
    | until_statement_modifier
    | for_statement_modifier
    | foreach_statement_modifier
    | when_statement_modifier

if_statement_modifier ::=    'if' expr {pin=1 name="Postfix if"}
unless_statement_modifier ::=    'unless' expr {pin=1 name="Postfix unless"}
while_statement_modifier ::=    'while' expr {pin=1 name="Postfix while"}
until_statement_modifier ::=    'until' expr {pin=1 name="Postfix until"}
for_statement_modifier ::=    'for' expr  {pin=1 name="Postfix for"}
foreach_statement_modifier ::=    'foreach' expr {pin=1 name="Postfix foreach"}
when_statement_modifier ::=    'when' expr {pin=1 name="Postfix when"}

private use_statements ::= // fixme these shold be recovered otherwise, cause it has no statement modifiers
  use_statement_constant
  | use_vars_statement
  | use_statement

private no_statements ::= // fixme these shold be recovered otherwise, cause it has no statement modifiers
  no_statement

upper use_statement ::=  'use' use_no_parameters {pin=1}
upper no_statement ::=  'no' use_no_parameters {pin=1}

private use_no_parameters ::= use_module_parameters | use_version_parameters
private use_module_parameters ::= default_package [perl_version [comma]] [<<parseStringifiedExpression>>];
private use_version_parameters ::= perl_version;

upper use_statement_constant ::=  'use' <<package_expr 'constant'>> [perl_version] [use_constant_parameters] {pin=2}
private use_constant_parameters ::=
  constants_block
  | single_constant_definition
  | expr

// other name to extend declaration, not variable
upper use_vars_statement ::=  'use' <<package_expr 'vars'>> [perl_version] [<<parseUseVarsParameters>>] {pin=2}

constants_block ::= '{' [constants_block_content] '}' {pin=1}
private constants_block_content ::= constant_block_content_element (comma constant_block_content_element) * [comma+] {recoverWhile=recover_constants_block}
private recover_constants_block ::= !('}')
private constant_block_content_element ::= constant_definition | scalar_expr {recoverWhile=recover_single_constant_definition}
private recover_single_constant_definition ::= !('}'|comma|';')

private single_constant_definition ::= constant_definition {recoverWhile=recover_statement}

constant_definition ::= string comma scalar_expr

undef_expr ::=  'undef' (undef_params | '(' undef_params ')') ? {pin=1}
private undef_params ::= deref_expr | variable

require_expr ::=  'require' (require_package| perl_version | scalar_expr)  {pin=1}// multiline string is possible too

private require_package ::= default_package !'('
private default_package ::= <<package_expr IDENTIFIER>>
meta package_expr ::= <<x1>>

private recover_statement ::= <<recoverStatement>>

// expression
expr ::=
    lp_or_xor_expr          // 0
    | lp_and_expr           // 1
    | lp_not_expr           // 2
    | named_list_expr       // 3
    | comma_sequence_expr   // 4
//    | comma_expr            // 4
    | assign_or_flow_expr   // 5
    | trenar_expr           // 6
    | flipflop_expr         // 7
    | or_expr               // 8
    | and_expr              // 9
    | bitwise_or_xor_expr   // 10
    | bitwise_and_expr      // 11
    | equal_expr            // 12
    | compare_expr          // 13
    | named_unary_expr      // 14
    | shift_expr            // 15
    | add_expr              // 16
    | mul_expr              // 17
    | regex_expr            // 18
    | op_5_expr             // 19
    | pow_expr              // 20
    | op_3_expr             // 21
    | deref_expr            // 22
    | term_expr             // 23

// above list operators
private list_expr ::= <<parseExpressionLevel 3>>

// List expression elements
private scalar_expr ::= <<parseExpressionLevel 4>>

// Unary expression argument
private unary_expr ::= <<parseExpressionLevel 14>>

// used in local declaration, fixme legacy
private immutable_expr ::= <<parseExpressionLevel 21>>

named_unary_expr ::= { '-t' | 'unary' } [unary_expr]
named_list_expr ::= 'argumentless' // fixme implement

// ordered for best performance of perltidy
term_expr ::=
    <<parseScalarOrElement>>	// scalar, cast or elements
    | string
    | number_constant
    | variable_declaration_lexical
    | <<parseListOrListElement>> // fixme this must be in array with qw
    | <<parseArrayOrSlice>>		// array, cast or slice
    | match_regex
    | return_expr
    | hash_variable
    | hash_cast_expr
    | array_index_variable
	| scalar_index_cast_expr
    | anon_array
    | undef_expr
    | print_expr
	| replacement_regex
    | sub_expr
    | eval_expr
    | do_expr
	| anon_hash
    | variable_declaration_local
    | sort_expr
    | grep_expr
    | trycatch_expr
    | map_expr

    | <<parseGlobOrElement>>	// glob
    | tag_scalar
    | variable_declaration_global
    | compile_regex
	| tr_regex
    | file_read_expr
    | require_expr
    | perl_handle_expr
	| <<parseParserExtensionTerm>>
    | sub_call_expr


grep_expr ::=  'grep' grep_map_arguments {pin=1}
map_expr ::=  'map' grep_map_arguments {pin=1}

private grep_map_arguments ::=
    '(' grep_map_arguments_ ')' !'['
    | grep_map_arguments_

private grep_map_arguments_ ::=
    parse_block [comma] grep_map_sort_tail
    | scalar_expr comma grep_map_sort_tail
    | expr

private grep_map_sort_tail ::= list_expr

sort_expr ::=  'sort' sort_arguments {pin=1}
private sort_arguments ::=
    '(' sort_arguments_ ')' !'['
    | sort_arguments_

private sort_arguments_ ::=
    scalar_variable grep_map_sort_tail
    | parse_block [comma] grep_map_sort_tail
    | callable grep_map_sort_tail
    | grep_map_sort_tail

parenthesised_expr ::= '(' parenthesised_expr_content ')' {pin=1 name="Parenthesised expression"}
private parenthesised_expr_content ::= [expr] {recoverWhile=recover_parenthesised}
private recover_parenthesised ::= !(')' | '{' | '}' | <<checkSemicolon>> )

deref_expr ::= expr (<<parseArrowSmart>> nested_element_variation) + //{pin(".*")=1}

private op_3_expr ::= pref_pp_expr | pref_mm_expr | suff_pp_expr
pref_pp_expr ::= '++' expr
pref_mm_expr ::= '--' expr

suff_pp_expr ::= expr ('++'|'--')

pow_expr ::= expr ('**' expr)+ { rightAssociative=true }

private op_5_expr ::= ref_expr | prefix_unary_expr
ref_expr ::= '\\' referencable_expr { rightAssociative=true }
prefix_unary_expr ::= {'~' | '!' | 'u+' | 'u-'} expr { rightAssociative=true }
regex_expr ::= expr ('=~'|'!~') expr
mul_expr ::= expr ({'*'|'/'|'%'|'x'} expr)+
add_expr ::= expr ({'+'|'-'|'.'} expr)+
shift_expr ::= expr ({'<<'|'>>'} expr)+
compare_expr ::= expr ({'>='|'<='|'>'|'<'|'ge'|'le'|'gt'|'lt'}) expr
equal_expr ::= expr {'=='|'!='|'<=>'|'eq'|'ne'|'cmp'|'~~'} expr
bitwise_and_expr ::= expr ('&' expr)+
bitwise_or_xor_expr ::= expr ({'|'|'^'} expr)+
and_expr ::= expr ( '&&' expr)+
or_expr ::= expr ( {'||'|'//'} expr)+
flipflop_expr ::= expr ('..'|'...') expr
trenar_expr ::= expr '?' scalar_expr ':' scalar_expr { rightAssociative=true }

private assign_or_flow_expr ::=
    assign_expr
    | last_expr
    | next_expr
    | goto_expr
    | redo_expr

// fixme do we need to collapse tokens?
assign_expr ::= expr ({'**='|'+='|'-='|'*='|'/='|'%='|'.='|'x='|'&='|'|='|'^='|'<<='|'>>='|'&&='|'||='|'//='|'='} expr ) + { rightAssociative=true }
comma_sequence_expr ::= expr <<parseCommaSequence>> // fixme implement pinnning
lp_not_expr ::= 'not' expr { rightAssociative=true }
lp_and_expr ::= expr ('and' expr)+
lp_or_xor_expr ::= expr ({'or'|'xor'} expr)+

print_expr ::=  ('print'|'printf'|'say') ( '(' [print_arguments] ')' | [print_arguments] ) {pin=1}
private print_arguments ::= perl_handle expr | expr

sub_expr ::=  'sub' sub_definition_parameters parse_block  // fixme make sure that this one checked after definition and declaration

sub_call_expr ::= leftward_call_expr | rightward_call_expr

private leftward_call_expr ::= callable <<parseLeftwardCallArguments>>
private rightward_call_expr ::= callable [call_arguments]

//// fixme this should depend on prototype
call_arguments ::= parse_call_arguments
private parse_call_arguments ::=
    anon_hash comma list_expr
    | parse_block [[comma] list_expr]
    | list_expr

file_read_expr ::= LEFT_ANGLE perl_handle_expr RIGHT_ANGLE

////////////////////////// regular expressions /////////////////////////////////////////////////////////////////////////
// pinning quotes leads to bug with replacement block
compile_regex ::=   'qr' match_regex_body {pin=1}
match_regex ::=   ['m'] match_regex_body
private match_regex_body ::= regex_match REGEX_QUOTE_CLOSE [perl_regex_modifiers]

replacement_regex ::=
     's'
    regex_match
    regex_replace
    REGEX_QUOTE_CLOSE
    [perl_regex_modifiers]
    {pin=1}

private regex_match ::= REGEX_QUOTE_OPEN [LP_REGEX|LP_REGEX_X|perl_regex]
private regex_replace ::= regex_replace_regex | regex_replace_code

private regex_replace_regex ::= {REGEX_QUOTE | REGEX_QUOTE_CLOSE REGEX_QUOTE_OPEN} [LP_REGEX_REPLACEMENT|perl_regex]
private regex_replace_code ::= {REGEX_QUOTE_E | REGEX_QUOTE_CLOSE REGEX_QUOTE_OPEN_E} [regex_code]

private regex_code ::= LP_CODE_BLOCK | file_item+ {recoverWhile=recover_regex_code}
private recover_regex_code ::= !(REGEX_QUOTE_CLOSE)

perl_regex_modifiers ::= REGEX_MODIFIER +
perl_regex ::= <<parseRegexContent 'false'>>
private perl_regex_items ::= perl_regex_item +

private perl_regex_item ::= interpolated_constructs | <<convertRegexToken>>

perl_regex_ex ::= <<parseRegexContent 'true'>> {extends=perl_regex}
private perl_regex_ex_items ::= perl_regex_item_ex +

private perl_regex_item_ex ::= interpolated_constructs | <<convertRegexTokenEx>>

tr_regex ::=  ('tr'|'y') tr_search tr_replacement [tr_modifiers] {pin=1}
private tr_search ::= REGEX_QUOTE_OPEN [tr_searchlist] {pin=1}
tr_searchlist ::= STRING_CONTENT
private tr_replacement ::= {REGEX_QUOTE | REGEX_QUOTE_CLOSE REGEX_QUOTE_OPEN} [tr_replacementlist] REGEX_QUOTE_CLOSE {pin=1}
tr_replacementlist ::= STRING_CONTENT
tr_modifiers ::= REGEX_MODIFIER +

////////////////////////// end of regular expressions //////////////////////////////////////////////////////////////////

do_expr ::=  'do' eval_argument {pin=1}
eval_expr ::=  'eval' [eval_argument] {pin=1}
private eval_argument ::= parenthesised_expr | parse_block | expr

private variable_declaration ::=
    variable_declaration_global
    | variable_declaration_lexical
    | variable_declaration_local

// @todo attributes support
variable_declaration_local ::=  'local' [default_package]  local_variable_declaration_variation {pin=1}
variable_declaration_lexical ::=  ('my' | 'state') [default_package] variable_declaration_variation [var_attributes] {pin=1}
variable_declaration_global ::=  'our' [default_package] variable_declaration_variation [var_attributes] {pin=1}


private local_variable_declaration_variation ::= local_parenthesised_declaration | local_variable_declaration_argument
private local_parenthesised_declaration ::= '(' local_variable_declaration_argument (comma + local_variable_declaration_argument ) * comma * ')' {pin=1}
private local_variable_declaration_argument ::= strict_variable_declaration_argument | scalar_expr

private variable_declaration_variation ::= variable_parenthesised_declaration | variable_declaration_argument
private variable_parenthesised_declaration ::= '(' variable_parenthesised_declaration_contents ')' {pin=1}
private variable_parenthesised_declaration_contents ::= strict_variable_declaration_argument (comma + strict_variable_declaration_argument ) * comma*

private strict_variable_declaration_argument ::= strict_variable_declaration_wrapper |  'undef'
private variable_declaration_argument ::= variable_declaration_wrapper |  'undef'

private strict_variable_declaration_wrapper ::= variable_declaration_wrapper !('{' | '[' | '->' )
variable_declaration_wrapper ::= lexical_variable

/////////////////////////////////// REFERENCES /////////////////////////////////////////////////////////////////////////
anon_array ::= '[' [expr] ']' //{pin=1}
anon_hash ::= '{' [expr] '}' <<validateAnonHashSuffix>>  // fixme pin here after solving problem with code block and prototypes

//////////////////////////// END OF REFERENCES /////////////////////////////////////////////////////////////////////////

// fixme it's not a variable, its variable expression
private variable ::= <<parseScalarOrElement>> | <<parseArrayOrSlice>> | hash_variable | hash_cast_expr | <<parseGlobOrElement>>

private array_primitive ::=
    array_variable
    | array_cast_expr
    | string_list

array_array_slice ::= array_primitive array_index
array_hash_slice ::= array_primitive hash_index

array_cast_expr ::= '$@' cast_target

hash_cast_expr ::= '$%' cast_target

private scalar_primitive ::=
    scalar_variable
    | scalar_cast_expr
    | undef_expr  // shouldn't it be in term ? (check declarations)

scalar_cast_expr ::= '$$' cast_target

scalar_index_cast_expr ::= '$#' cast_target

// fixme need optimization
private cast_target ::= scalar_primitive | parse_block

scalar_array_element ::= scalar_primitive array_index
scalar_hash_element ::= scalar_primitive hash_index
anon_array_element ::= parenthesised_expr array_index

private glob_primitive ::=
    glob_variable
    | glob_cast_expr

glob_cast_expr ::= '$*' cast_target

glob_slot ::= glob_primitive hash_index

// extended nested element for using in ()
private nested_element_variation ::=
	<<parseNestedElementVariation>>
    | hash_index
    | array_index
    | <<nested_call IDENTIFIER>>
    | nested_call_arguments    // function call like $var->()
    | scalar_call

meta nested_call ::= <<parse_nested_call <<x1>>>>
private meta parse_nested_call ::= <<method <<x1>>>> [method_call_arguments]
scalar_call ::= <<parseScalarOrElement>> [method_call_arguments]
private method_call_arguments ::= '(' [<<parseSimpleCallArguments>>] ')'
nested_call_arguments ::= method_call_arguments

hash_index ::= <<parseHashIndex>>
array_index ::= <<parseArrayIndex>>

///////////////////////////////////// CALLABLE /////////////////////////////////////////////////////////////////////////

private callable ::= normal_method | code // being used in parseFunctionCall
private referencable_expr ::= code | scalar_expr

private code ::=
    '$&' normal_method
    | code_variable
    | code_cast_expr

code_cast_expr ::= SIGIL_CODE cast_target

////////////////////////////////END OF CALLABLE ////////////////////////////////////////////////////////////////////////

label_declaration ::= <<parseLabelDeclaration>> // do we need to change tokentype?
private perl_version ::= <<parsePerlVersion>>
private perl_handle ::=  perl_handle_expr | parse_block | scalar_variable !('{'|'['|<<isOperatorToken>>)
perl_handle_expr ::= HANDLE

//////////////////////////////////// constants /////////////////////////////////////////////////////////////////////////

tag_scalar ::= TAG

number_constant ::= NUMBER | NUMBER_VERSION | NUMBER_SIMPLE

private string ::= string_bare | string_sq | string_dq | string_xq | heredoc_opener

string_dq ::= [ 'qq'] quoted_qq_string
private quoted_qq_string ::= QUOTE_DOUBLE_OPEN [LP_STRING_QQ | qq_string_content ] QUOTE_DOUBLE_CLOSE {pin=1}
private qq_string_content ::= (STRING_CONTENT_QQ | interpolated_constructs)+

string_xq ::= [ 'qx'] quoted_xq_string
private quoted_xq_string ::= QUOTE_TICK_OPEN [LP_STRING_XQ | xq_string_content] QUOTE_TICK_CLOSE {pin=1}
private xq_string_content ::= (STRING_CONTENT_XQ | interpolated_constructs)+

private interpolated_constructs ::=
  <<parseScalarOrElement>>
  | <<parseArrayOrSlice>>
  | array_index_variable
  | scalar_index_cast_expr

string_bare ::= <<parseAndWrapStringContent>>

string_sq ::= [ 'q'] <<parseSQString>>	// fixme use vars hack, remove
string_list ::=  'qw' <<parseSQString>>
private quoted_sq_string ::= QUOTE_SINGLE_OPEN [LP_STRING_Q |LP_STRING_QW|sq_string_content] QUOTE_SINGLE_CLOSE {pin=1}
private sq_string_content ::= STRING_CONTENT+


private use_vars_interpolated_constructs ::=
    scalar_variable
    | array_variable
    | hash_variable
    | glob_variable
    | code_variable

heredoc_opener ::= 'heredoc<<' ( '\\' string_bare | string ){pin=1}

///////////////////////////////////// variables ////////////////////////////////////////////////////////////////////////
private lexical_variable ::= scalar_variable | array_variable | hash_variable

array_index_variable ::= '$#' SCALAR_NAME
scalar_variable ::= '$$' SCALAR_NAME
array_variable ::= '$@' ARRAY_NAME
hash_variable ::= '$%' HASH_NAME
code_variable ::= '$&' CODE_NAME
glob_variable ::= '$*' GLOB_NAME

private comma ::= ',' | '=>'
private normal_method ::= <<method IDENTIFIER>>
meta method ::= <<x1>>


/********************************************** Extensions for Method::Signatures *************************************/
// we can make this smareter and use keywords from settings or import opitions; We can't pin here because MooseX method works othewise
method_definition ::= 'method' IDENTIFIER [method_signature] [sub_attributes] parse_block
func_definition ::= 'func' IDENTIFIER [func_signature] [sub_attributes] parse_block

// not sure that we need a wrapper for signatures
private method_signature ::= '(' method_signature_content ')' {pin=1}
method_signature_content ::= [method_signature_invocant] [variable_parenthesised_declaration_contents] {recoverWhile=recover_signature_content}
method_signature_invocant ::= <<scalarDeclarationWrapper>> ':'
private func_signature ::= '(' func_signature_content ')' {pin=1}
func_signature_content ::= [variable_parenthesised_declaration_contents] {recoverWhile=recover_signature_content}
private recover_signature_content ::= !(')'|'{')

/********************************************** Extensions for Moose **************************************************/
