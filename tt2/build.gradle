/*
 * Copyright 2015-2021 Alexandr Evstigneev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.jetbrains.grammarkit.tasks.GenerateLexerTask
import org.jetbrains.grammarkit.tasks.GenerateParserTask

intellij {
  type.set('IC')
  plugins = [project(':plugin'), 'java']
}

dependencies {
  [":plugin:core"].each {
    compileOnly project(it).sourceSets.main.output
    testCompileOnly project(it).sourceSets.main.output
  }
  [":plugin"].each { testImplementation project(it).sourceSets.test.output }
}

tasks.register('generateTT2Parser', GenerateParserTask) {
  source.set("grammar/TemplateToolkit.bnf")
  pathToParser.set('/com/perl5/lang/tt2/parser/TemplateToolkitParserGenerated.java')
  pathToPsiRoot.set('/com/perl5/lang/tt2/psi')
  targetRoot.set(genRoot.canonicalPath)
  purgeOldFiles.set(true)
}


tasks.register('generateTT2Lexer', GenerateLexerTask) {
  dependsOn generateTT2Parser
  source.set("grammar/TemplateToolkit.flex")
  targetDir.set("src/main/gen/com/perl5/lang/tt2/lexer/")
  targetClass.set("TemplateToolkitLexerGenerated")
  skeleton.set(rootProject.file(lexer_skeleton))
  purgeOldFiles.set(true)
}

compileJava {
  dependsOn generateTT2Lexer
}
