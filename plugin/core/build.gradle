/*
 * Copyright 2015-2021 Alexandr Evstigneev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.jetbrains.grammarkit.tasks.GenerateLexerTask
import org.jetbrains.grammarkit.tasks.GenerateParserTask

intellij {
  type.set('IC')
  intellij.plugins = ['java']
}

tasks.register('generatePerl5Parser', GenerateParserTask) {
  source.set("grammar/Perl5.bnf")
  pathToParser.set('/com/perl5/lang/perl/parser/PerlParserGenerated.java')
  pathToPsiRoot.set('/com/perl5/lang/perl/psi')
}

tasks.register('generatePodParser', GenerateParserTask) {
  source.set("grammar/Pod.bnf")
  pathToParser.set('/com/perl5/lang/pod/parser/PodParserGenerated.java')
  pathToPsiRoot.set('/com/perl5/lang/pod/psi')
}


tasks.register('generatePerlLexer', GenerateLexerTask) {
  dependsOn generatePerl5Parser
  source.set("grammar/Perl.flex")
  targetDir.set("src/main/gen/com/perl5/lang/perl/lexer/")
  targetClass.set("PerlLexer")
}

tasks.register('generatePodLexer', GenerateLexerTask) {
  dependsOn generatePodParser
  source.set("grammar/Pod.flex")
  targetDir.set("src/main/gen/com/perl5/lang/pod/lexer/")
  targetClass.set("PodLexerGenerated")
}

tasks.withType(GenerateLexerTask).each {
  it.skeleton.set(rootProject.file(lexer_skeleton))
  it.purgeOldFiles.set(true)
}

tasks.withType(GenerateParserTask).each {
  it.targetRoot.set(genRoot.canonicalPath)
  it.purgeOldFiles.set(true)
}

compileJava {
  dependsOn generatePerlLexer
  dependsOn generatePodLexer
}

